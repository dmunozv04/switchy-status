//file:noinspection GroovyAccessibility
//file:noinspection GroovyAssignabilityCheck
buildscript {
}

plugins {
	id 'maven-publish'
	alias libs.plugins.loom
}

archivesBaseName = project.archives_base_name

def mc_major = '1.19'
def mc = mc_major + '.2'
def default_branch = '1.19'

def origVersion = project.version
project.version = project.version + "+" + mc_major
group = project.maven_group

repositories {
	mavenLocal()
	maven { url "https://maven.proxyfox.dev/" }
	maven { url "https://maven.terraformersmc.com/" }
	maven { url "https://maven.ladysnake.org/releases" }
	maven { url "https://maven.nucleoid.xyz/" }
	maven { url "https://maven.wispforest.io" }
}

dependencies {
	// // Base // //
	minecraft libs.mc
	mappings variantOf(libs.qm) { classifier "intermediary-v2" }
	modImplementation libs.ql
	modImplementation libs.qfapi

	modImplementation libs.switchyCore
	modImplementation libs.switchyClient
	modImplementation libs.switchyCardinal
	modImplementation libs.switchyUI

}

processResources {
	final Map<String, String> map = [
		"version" : version.toString(),
		"mc"      : libs.versions.mc.get(),
		"fl"      : libs.versions.fl.get(),
		"ql"      : libs.versions.ql.get(),
		"fapi"    : libs.versions.fapi.get(),
		"qfapi"   : libs.versions.qfapi.get(),
		"switchy" : libs.versions.switchy.get(),
	]

	inputs.properties(map)
	filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE.txt") {
		rename { "${it}_${archivesBaseName}" }
	}
	from("src/main/resources/data/switchy_status/lang/en_us.json") {
		into { "assets/switchy_status_data/lang" }
	}
}
